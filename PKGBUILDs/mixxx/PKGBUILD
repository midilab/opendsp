# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=2.1.5
pkgrel=2
pkgdesc="Free, open source software for digital DJ'ing."
arch=('armv7h')
url='https://www.mixxx.org'
license=('GPL')
depends=('fftw' 'libid3tag' 'libmad' 'soundtouch' 'libogg' 'libshout' 'libsndfile' 'portaudio' 'portmidi'
         'taglib' 'qt4' 'vamp-plugin-sdk' 'libusbx' 'protobuf' 'faad2' 'libmp4v2'
         'rubberband' 'chromaprint' 'sqlite' 'opus' 'opusfile' 'upower' 'hidapi' 'libebur128')
makedepends=('mesa' 'scons' 'libshout' 'glu')
source=("https://github.com/${pkgname}dj/${pkgname}/archive/release-${pkgver}.tar.gz")
md5sums=('49e1257c206143640ff4c7aff8fa3d83')

build() {
  cd "${srcdir}/${pkgname}-release-${pkgver}"
  export SCONSFLAGS="-j $(nproc)"
  sed -i 's/mp4.h/mp4v2\/mp4v2.h/' plugins/soundsourcem4a/soundsourcem4a.h

  scons build=release optimize=native opengles=1 virtualize=0 ffmpeg=1 localecompare=0 qt_sqlite_plugin=1 opus=1 qtdir=/usr/lib/qt4 prefix=/usr faad=1 mad=1 hid=1 
}

package() {
  cd "${srcdir}/${pkgname}-release-${pkgver}"
  scons qtdir=/usr/lib/qt4 prefix=/usr install_root="${pkgdir}/usr" install
}
