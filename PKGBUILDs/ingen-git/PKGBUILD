# Maintainer: midilab <contact at midilab dot co>
_pkgname=ingen
pkgname="${_pkgname}-git"
pkgver=0.5.1.r2843.e2d69c69
pkgrel=1
pkgdesc="A modular plugin host for JACK and LV2."
arch=('armv7h')
url="http://drobilla.net/software/${_pkgname}/"
license=('GPL')
depends=('boost' 'alsa-lib' 'ganv' 'jack' 'lilv' 'portaudio' 'suil' 'serd' 'sord' 'raul' 'lv2')
makedepends=('python' 'python-markdown' 'python-pygments' 'python-rdflib')
optdepends=('lv2-plugins: various useful LV2 plug-in packages')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+https://github.com/drobilla/ingen.git/")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/${_pkgname}"

  local ver=$(grep ^INGEN_VERSION wscript | cut -f 2 -d "'")
  local rev=$(git rev-list --count HEAD)
  local githash=$(git rev-parse --short HEAD)
  echo "${ver}.r${rev}.${githash}"
}

build() {
  cd "${srcdir}/${_pkgname}"
  meson setup build
  cd build
  #meson configure -Dcpp_args="-mtune=native -march=native" -Dprefix="/usr"
  meson configure -Dprefix="/usr"
  meson compile
}

package() {
  cd "${srcdir}/${_pkgname}/build/"
  DESTDIR="${pkgdir}/" meson install
}
