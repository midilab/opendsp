# Maintainer: Midilab <aur -at- midilab -dot- co>

_pkgname=ingen
pkgname="${_pkgname}-git"
pkgver=0.5.1.r2843.e2d69c69
pkgrel=1
pkgdesc="A modular plugin host for JACK and LV2."
arch=('arm' 'armv6h' 'armv7h' 'aarch64')
url="http://drobilla.net/software/${_pkgname}/"
license=('GPL')
depends=('alsa-lib' 'ganv' 'jack' 'lilv' 'portaudio' 'suil' 'serd' 'sord' 'raul' 'lv2')
makedepends=('python')
optdepends=(
    'lv2-plugins: various useful LV2 plug-in packages'
)
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+https://github.com/drobilla/ingen.git/"
        "wscript.force.patch")
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd "$srcdir/${_pkgname}"

  local ver=$(grep ^INGEN_VERSION wscript | cut -f 2 -d "'")
  local rev=$(git rev-list --count HEAD)
  local githash=$(git rev-parse --short HEAD)
  echo "${ver}.r${rev}.${githash}"
}

prepare(){
  cd "${srcdir}/${_pkgname}"
  git clone https://github.com/drobilla/autowaf.git waflib

  # patch file to fix error on those that dont have the HAVE_POSIX_MEMALIGN
  sed -i 's/LV2_buf/void/' src/server/Buffer.cpp  

  # on some platforms autowaf are buggy for sockets and jack related
  patch < ../wscript.force.patch  
}

build() {
  cd "$srcdir/${_pkgname}"

  python waf configure --prefix=/usr
  python waf build
}

package() {
  cd "$srcdir/${_pkgname}"

  python waf install --destdir="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
