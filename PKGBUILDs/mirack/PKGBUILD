pkgname='vcvrack'
pkgver=1.0.0
pkgrel=1
pkgdesc='Open-source virtual modular synthesizer'
url='https://vcvrack.com/'
license=(BSD)
arch=(armv7h i686 x86_64)
provides=(vcvrack-fundamental)
conflicts=(vcvrack-fundamental)
depends=(curl glew gtk2 jansson libzip openssl rtmidi speexdsp
    libxi libxrandr libxinerama libxcursor libgl
    jack libpulse)
makedepends=(git cmake mesa zip)

source=(
    git+https://github.com/mi-rack/Rack.git
    vcvrack.sh
)
sha256sums=(
    SKIP
    ed2da5d924a381cd50125db199578c15bc95c18be716abbd3ff310bbcf6773c0
)

prepare() {
    cd "$srcdir/Rack"
    #git submodule update
    git submodule sync 
    git submodule update --init --recursive
}

build() {
    cd "$srcdir/Rack"
    make dep
    CFLAGS="$CFLAGS $(pkg-config --cflags glew jansson libcurl libzip openssl rtmidi speexdsp)" make RELEASE=1
    make
    make +Fundamental    
}

package() {
    cd "$srcdir/Rack"
    install -D -m755 "$srcdir/vcvrack.sh" "$pkgdir/usr/bin/vcvrack"
    install -D -m644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
    install -D -m755 -t "$pkgdir/opt/$pkgname" Rack
    install -D -m755 -t "$pkgdir/opt/$pkgname/lib" dep/lib/lib*
    install -D -m644 -t "$pkgdir/opt/$pkgname" Core.json
    cp -dr --preserve=mode -t "$pkgdir/opt/$pkgname" dep/include
    cp -dr --preserve=mode -t "$pkgdir/opt/$pkgname" res

    cd "$srcdir/Rack/plugins/Fundamental"
    install -D -m644 dist/Fundamental-*-lin.zip "$pkgdir/opt/vcvrack/Fundamental.zip"
}
