# Maintainer: Christopher Arndt <aur -at- chrisarndt -dot- de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

_pkgname=calf
pkgname="${_pkgname}-git"
pkgver=0.90.1.r2534.e5c08dc2
pkgrel=1
pkgdesc="LV2/JACK audio plug-ins for musicians (git version)"
arch=('armv7h' 'i686' 'x86_64')
url="http://calf-studio-gear.org/"
license=('GPL' 'LGPL')
depends=('fluidsynth' 'lv2-git') 
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
#install="$pkgname.install"
source=("${_pkgname}::git+https://github.com/calf-studio-gear/calf")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  version=$(awk -F '[][]' '/AC_INIT/{print $4}' configure.ac)
  revision=$(git rev-list --count HEAD)
  hash=$(git rev-parse --short HEAD)
  echo $version.r$revision.$hash
}

build() {
  cd "${srcdir}/${_pkgname}"

  # fix for archlinux package naming and version problem
  sed -i 's/lv2core >= 6/lv2 >= 1.15/' configure.ac

  ./autogen.sh \
    --prefix="/usr" \
    --enable-static=no \
    --enable-experimental \
    --disable-sse

  export CFLAGS="$CFLAGS -O3"
  export CXXFLAGS="$CXXFLAGS -O3"

  sed -i 's/<lv2.h>/"\/usr\/include\/lv2\/lv2.h"/' src/makerdf.cpp
  sed -i 's/"lv2.h"/"\/usr\/include\/lv2\/lv2.h"/' src/calf/lv2_options.h
  sed -i 's/"lv2.h"/"\/usr\/include\/lv2\/lv2.h"/' src/calf/lv2_ui.h
  sed -i 's/<lv2.h>/"\/usr\/include\/lv2\/lv2.h"/' src/calf/lv2wrap.h

  make
}

package(){
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="$pkgdir/" install
}
