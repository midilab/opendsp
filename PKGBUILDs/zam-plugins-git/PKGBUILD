# Maintainer Haskellfant <moritz.kiefer@purelyfunctional.org
pkgname=zam-plugins-git
pkgver=3.10.r13.gfbdbf08
pkgrel=1
pkgdesc="Collection of LV2/LADSPA/VST audio plugins for high quality processing"
arch=('armv7h' 'i686' 'x86_64')
url="https://github.com/zamaudio/zam-plugins"
license=('GPL2')
provides=('zamplugins')
depends=('gcc-libs')
makedepends=('git' 'pkg-config' 'libx11' 'libgl' 'liblo' 'jack' 'ladspa')
optdepends=()
source=("$pkgname"::'git://github.com/zamaudio/zam-plugins.git'
        "dpf"::'git://github.com/DISTRHO/DPF.git')
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/$pkgname"
  git submodule init
  git config submodule.dpf.url "$srcdir/dpf"
  git submodule update
}

build() {
  cd "$srcdir/$pkgname"
  export NOOPT=true
  export CFLAGS="$CFLAGS -O3"
  export CXXFLAGS="$CXXFLAGS -O3"
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir/" PREFIX=/usr install
}
