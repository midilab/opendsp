# Maintainer: Midilab <contact@gmidilab.co>

pkgname=mididings
pkgver=r706.bbec99a
pkgrel=1
pkgdesc="A MIDI router and processor based on Python, supporting ALSA and JACK MIDI"
arch=('armv5h' 'armv6h' 'armv7h' 'armv8h')
license=('GPL')
depends=('boost-libs' 'python-decorator' 'jack')
makedepends=('boost')
optdepends=('python-pyliblo: to send or receive OSC messages'
            'python-pyinotify: to automatically restart when a script changes')
source=("${pkgname}::git://github.com/dsacre/mididings.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/$pkgname"
  
  # for python3.7 async is now a reserved word, lets patch it
  sed -i 's/async/a_sync/' mididings/units/call.py
}

build() {
  cd "$srcdir/$pkgname"
  ./setup.py build #--enable-smf
}

package() {
  cd "$srcdir/$pkgname"
  python setup.py install --prefix=/usr --root=$pkgdir

  # docs
  install -d "$pkgdir/usr/share/doc/$pkgname/examples"
  install -Dm644 doc/*.* \
    "$pkgdir/usr/share/doc/$pkgname"

  # examples
  install -Dm644 doc/examples/* \
    "$pkgdir/usr/share/doc/$pkgname/examples"
}
