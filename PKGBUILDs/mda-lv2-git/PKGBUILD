# Maintainer: Christopher Arndt <aur -at- chrisarndt -dot- de>

_pkgname=mda-lv2
pkgname="${_pkgname}-git"
pkgver=1.2.2.r197.dc11f75
pkgrel=1
pkgdesc="A port of the MDA VST plugins to the LV2 format"
arch=("armv7h" "i686" "x86_64")
url="http://drobilla.net/software/mda-lv2/"
license=("GPL2")
groups=('lv2-plugins')
depends=('lv2-git')
makedepends=('git' 'python')
provides=("${_pkgname}")
conflicts=("${_pkgname}" "${_pkgname}-svn")
source=("${_pkgname}::git+http://git.drobilla.net/mda.lv2.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"

  local ver=`grep "^MDA_VERSION" wscript | cut -d "'" -f 2`
  echo "$ver.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}"

  python ./waf configure --prefix="/usr"

  sed -i 's/lv2.h/\/usr\/include\/lv2\/lv2.h/' lvz/wrapper.cpp
  python ./waf build
}

package() {
  cd "${srcdir}/${_pkgname}"

  python ./waf install --destdir="${pkgdir}"
}
