# Maintainer: Midilab <contact@midilab.co>
pkgname=non-daw-git
_gitname=non
pkgver=0.r1845.c15bfa8
pkgrel=1
pkgdesc="A modular Digital Audio Workstation system - Git version."
arch=('armv7h')
url="http://non.tuxfamily.org"
license=('GPL')
groups=('multimedia')
depends=('jack' 'liblo' 'libsigc++' 'liblrdf' 'hicolor-icon-theme' 'ntk')
makedepends=('git' 'python' 'libxpm')
provides=('non-timeline' 'non-mixer' 'non-session-manager' 'non-sequencer')
conflicts=('non-daw' 'non-timeline' 'non-mixer' 'non-session-manager' 'non-sequencer')
source=('git+https://github.com/linuxaudio/non.git')
md5sums=('SKIP')
install=non.install

CXXFLAGS="$CXXFLAGS -std=gnu++11"

pkgver() {
  cd "${srcdir}/${_gitname}"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./waf configure --prefix="/usr"
  ./waf
}

package(){
  cd "${srcdir}/${_gitname}"
  ./waf --destdir="${pkgdir}" install
  install -m 755 "${srcdir}/non/timeline/bin/import-external-sources" "${pkgdir}/usr/bin"
  install -m 755 "${srcdir}/non/timeline/bin/remove-unused-sources" "${pkgdir}/usr/bin"
}

# vim:set ts=2 sw=2 et:
