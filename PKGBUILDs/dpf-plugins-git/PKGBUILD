# Maintainer: CrocoDuck <crocoduck dot oducks at gmail dot com>

# ALARM: midilab <contact at midilab dot co>
#  - disable sse optmizations

pkgname=dpf-plugins-git
pkgver=r113.86084a9
pkgrel=2
pkgdesc="Collection of DPF-based plugins by DISTRHO. LV2, LADSPA, dssi and VST."
arch=('armv7h' 'i686' 'x86_64')
url="http://distrho.sourceforge.net/"
license=('custom')
depends=('jack' 'liblo' 'projectm')
makedepends=('git' 'wget')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname%-*}"::'git+https://github.com/DISTRHO/DPF-Plugins.git')
md5sums=('SKIP')

pkgver() {
  cd "${pkgname%-*}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${pkgname%-*}"

    # ALARM: Disable sse optmizations
    if [[ $CARCH == "armv7h" || $CARCH == "aarch64" ]]
    then
      sed -i 's/-msse//' dpf/Makefile.base.mk
      sed -i 's/-msse2//' dpf/Makefile.base.mk
      sed -i 's/-mfpmath=sse//' dpf/Makefile.base.mk
      sed -i 's/-mtune=generic//' dpf/Makefile.base.mk

      sed -i 's/-msse//' dpf/dgl/Makefile
      sed -i 's/-msse2//' dpf/dgl/Makefile
      sed -i 's/-mtune=generic//' dpf/dgl/Makefile
    fi

    make all && make all -C plugins/ProM
}

package() {
    cd "${pkgname%-*}"
    # Install the custom license:
    install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    # Install the package files:
	make DESTDIR="$pkgdir/" PREFIX="/usr" install
}